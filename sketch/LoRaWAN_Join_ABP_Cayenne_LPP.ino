/*  Simple ABP join for a TTN Japan with Cayenne LPP Format
 *  
 *  In setup() below please replace the argument to LoRaWAN.begin()
 *  with your appropriate region specific band:
 *  
 *  AS923 AU915 EU868 IN865 KR920 US915
 *  
 *  AU915/US915 networks have 64+8 channels. Typical gateways support only
 *  8 (9) channels. Hence it's a good idea to pick the proper channel
 *  subset via select via LoRaWAN.setSubBand(),
 *  
 *  EU868/IN865 have duty cycle restrictions. For debugging it makes sense
 *  to disable those via setDutyCycle(false);
 *  
 *  For an external antenna one should set the proper antenna gain
 *  (default is 1.04) via setAntennaGain().
 *  
 *  Please edit the keys below as they are just debugging samples.
 *  
 *  This example code is in the public domain.
 */

#include "STM32L0.h"
#include "LoRaRadio.h"
#include "LoRaWAN.h"

// Set the values generated by TTN console to the following items
const char *devAddr = "00000000";                         // Device Address
const char *nwkSKey = "00000000000000000000000000000000"; // Network Session Key
const char *appSKey = "00000000000000000000000000000000"; // App Session Key

void setup(void)
{
    pinMode(LED_BUILTIN, OUTPUT);
    digitalWrite(LED_BUILTIN, LOW);
    
    Serial.begin(9600);
    delay(1000);
    
    LoRaRadio.begin(923200000);
    LoRaRadio.setLnaBoost(true);
    LoRaWAN.begin(AS923);
    LoRaWAN.setMaxEIRP(13.0f);
    LoRaWAN.setADR(false);
    LoRaWAN.setDataRate(2);
    
    // Antenna Gain Default: +1.04dBi
    // LoRaWAN.setAntennaGain(1.04f); // ANT-SS900
    LoRaWAN.setAntennaGain(1.2f);     // ANT-916-CW-HWR-SMA
    
    // Antenna power 13dBm or less, ARIB STD-T108 regulations
    LoRaWAN.setTxPower(13.0f);
    
    // ABP authentication
    LoRaWAN.joinABP(devAddr, nwkSKey, appSKey);
    if (Serial == 1) Serial.println("JOIN( )");
}

void loop(void)
{
    if (LoRaWAN.joined() && !LoRaWAN.busy())
    {
      if (Serial == 1)
      {
        Serial.print("TRANSMIT( ");
        Serial.print("TimeOnAir: ");
        Serial.print(LoRaWAN.getTimeOnAir());
        Serial.print(", NextTxTime: ");
        Serial.print(LoRaWAN.getNextTxTime());
        Serial.print(", MaxPayloadSize: ");
        Serial.print(LoRaWAN.getMaxPayloadSize());
        Serial.print(", DR: ");
        Serial.print(LoRaWAN.getDataRate());
        Serial.print(", TxPower: ");
        Serial.print(LoRaWAN.getTxPower(), 1);
        Serial.print("dbm, UpLinkCounter: ");
        Serial.print(LoRaWAN.getUpLinkCounter());
        Serial.print(", DownLinkCounter: ");
        Serial.print(LoRaWAN.getDownLinkCounter());
        Serial.println(" )");
      }
      
      // Carrier sense operation before transmit, ARIB STD-T108 regulations
      while (!LoRaRadio.sense(-80, 5)) delay(50);
      
      // Transmit in CayenneLPP format
      // https://developers.mydevices.com/cayenne/docs/lora/#lora-cayenne-low-power-payload
      // Mt.Fuji WGS84
      LoRaWAN.beginPacket();
      LoRaWAN.write(0x01); // 1byte  Data channel: Ch.1
      LoRaWAN.write(0x88); // 1byte  Data type: GPS
      LoRaWAN.write(0x05); // 3bytes Latitude 35.3607
      LoRaWAN.write(0x65);
      LoRaWAN.write(0x47);
      LoRaWAN.write(0x15); // 3bytes Longitude 138.7274
      LoRaWAN.write(0x2B);
      LoRaWAN.write(0x0A);
      LoRaWAN.write(0x05); // 3bytes Altitude 3772
      LoRaWAN.write(0xC1);
      LoRaWAN.write(0x70);
      LoRaWAN.endPacket();
      
      // Transmission downtime, ARIB STD-T108 regulations
      delay(50);
    }
    
    digitalWrite(LED_BUILTIN, HIGH);
    delay(1000);
    digitalWrite(LED_BUILTIN, LOW);
    
    // Stop mode
    STM32L0.stop(9000);
}